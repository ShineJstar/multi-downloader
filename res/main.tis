include "libs/vlist.tis";

include "export.tis";
include "runtime/runtime.tis";
include "utils/utils.tis";

var EXIT_FLAG = false;  // 窗口退出标志


self.ready = function() {
    var meta = runtime.appmeta;

    $(aside .title).text = meta.appname;
    view.windowIcon = self.url("assets/img/icon.png");
    view.trayIcon({
        image: self.loadImage(self.url("assets/img/icon.png")),
        text: meta.appname,
    });
    utils.centerWindow();
}

class MainController : Element
{
    var currentRoute = "";

    function attached() {
        this.route($(aside li[href]:first-child).attributes["href"]);
    }

    function route(topage) {
        if (currentRoute != topage) {
            var v = $(#view);
            v.update(::v.load(self.url(topage)));
            currentRoute = topage;
            this.postEvent("routechange", topage);
        }
    }

    // route change
    event click $(aside li[href]) (evt,e) { 
        for (var li in $$(aside li[href])) {
            li.attributes.removeClass("active");
        };
        e.attributes.addClass("active");

        this.route(e.attributes["href"]); 
    }

    event toast (evt,e) { $(#notify).add({title:evt.data.type, message:evt.data.msg, type:evt.data.type}); }

    event notify-task (evt,e) {
        View.window({
            type: View.POPUP_WINDOW,
            caption: "notify",
            url:self.url("components/window-task-finish.htm"),
            parameters: evt.data,
        }); 
    }

    event exit-app { EXIT_FLAG = true; view.close() }
}

event closerequest (evt) {
    if (EXIT_FLAG === true) {
        view.close()
    } else {
        evt.cancel = true;
        var r = view.msgbox({
            type: #question,
            title: "退出",
            content: "确定要退出此应用吗？",
            buttons: [
                {id:#cancel, text:"取消", role:"cancel-button"},
                {id:#yes, text:"确定", role:"default-button"},
            ],
        });
        if (r === #yes) {
            runtime.postEvent("close-request");
        }
    }
}

view << event trayicon-click (evt) {
    if (evt.buttons == 1) {  // left mouse button
        if (view.windowState in [View.WINDOW_SHOWN, View.WINDOW_MAXIMIZED]) {
            view.windowState = View.WINDOW_HIDDEN;
        } else if (view.windowState == View.WINDOW_HIDDEN) {
            view.windowState = View.WINDOW_SHOWN;
            view.windowTopmost = true;
            view.windowTopmost = false;
        } else if (view.windowState == View.WINDOW_MINIMIZED) {
            view.windowState = View.WINDOW_SHOWN;
        }

    } else if (evt.buttons == 2) {  // right mouse button
        var (sx,sy) = view.box(#position,#client,#screen);
        self.popup($(menu#tray), 2, evt.x - sx, evt.y - sy);
    }
}

event click $(menu#tray > li#show-window) {
    if (view.windowState in [View.WINDOW_HIDDEN, View.WINDOW_MINIMIZED]) {
        view.windowState = View.WINDOW_SHOWN;
        view.windowTopmost = true;
        view.windowTopmost = false;
    }
}

event click $(menu#tray > li#exit-app) { EXIT_FLAG = true; view.close(); }
